<PROGR>::=<ALGO><PROCDEFS>
<PROCDEFS>::=,<PROC><PROCDEFS>|&
<PROC>::=p<DIGITS>{<PROGR>}
<DIGITS>::=<D><MORE>
<D>::=0|1|2|3|4|5|6|7|8|9
<MORE>::=<DIGITS>|&
<ALGO>::=<INSTR><COMMENT><SEQ>
<SEQ>::=;<ALGO>|&
<INSTR>::=<INPUT>|<OUTPUT>|<ASSIGN>|<CALL>|<LOOP>|<BRANCH>|h
<CALL>::=cp<DIGITS>
<ASSIGN>::=<NUMVAR>:=<NUMEXPR>|<BOOLVAR>:=<BOOLEXPR>|<STRINGV>:=<STRI>
<LOOP>::=w(<BOOLEXPR>){<ALGO>}
<BRANCH>::=i(<BOOLEXPR>)t{<ALGO>}<ELSE>
<ELSE>::=e{<ALGO>}|&
<NUMVAR>::=n<DIGITS>
<BOOLVAR>::=b<DIGITS>
<STRINGV>::=s<DIGITS>
<NUMEXPR>::=a(<NUMEXPR>,<NUMEXPRP>)|m(<NUMEXPR>,<NUMEXPRP>)|d(<NUMEXPR>,<NUMEXPRP>)|<NUMEXPRP>
<NUMEXPRP>::=<NUMVAR>|<DECNUM>
<DECNUM>::=0.00|<NEG>|<POS>
<NEG>::=-<POS>
<POS>::=<INT>.<D><D>
<INT>::=1<MORE>|2<MORE>|3<MORE>|4<MORE>|5<MORE>|6<MORE>|7<MORE>|8<MORE>|9<MORE>
<BOOLEXPR>::=<LOGIC>|<CMPR>
<LOGIC>::=<BOOLVAR>|T|F|^(<BOOLEXPR>,<BOOLEXPR>)|v(<BOOLEXPR>,<BOOLEXPR>)|!(<BOOLEXPR>)
<CMPR>::=E(<NUMEXPR>,<NUMEXPR>)|<(<NUMEXPR>,<NUMEXPR>)|>(<NUMEXPR>,<NUMEXPR>)
<STRI>::="@@@@@@@@@@@@@@@"
<COMMENT>::=*@@@@@@@@@@@@@@@*|&
<INPUT>::=g<NUMVAR>
<OUTPUT>::=<TEXT>|<VALUE>
<VALUE>::=o<NUMVAR>
<TEXT>::=r<STRINGV>